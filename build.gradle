buildscript {
    repositories {
        mavenCentral()
        maven {  url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.12.1"
    }
}

//For github releases
plugins {
    id 'net.researchgate.release' version '2.3.4'
}

group 'io.github.mrblobman'
version '1.0.0'

apply plugin: 'maven-publish'

allprojects {
    apply plugin: 'java'
    apply plugin: "com.github.hierynomus.license" //Ensure all files have the license header

    license {
        include '**/*.java'
        header = rootProject.file('LICENSE')
        strictCheck true
    }
    assemble.dependsOn licenseFormat

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_1_7
        targetCompatibility = JavaVersion.VERSION_1_7
    }

    repositories {
        mavenLocal()
        //Spigot
        maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
        mavenCentral()
    }
}

//Make sure that all of the nms sub projects depend on the core
subprojects.each { subproject ->
    if (subproject.name != 'core') {
        subproject.dependencies {
            compile project(':core')
        }
    }
}

jar {
    subprojects.each { subproject ->
        from subproject.sourceSets.main.output
    }
}

task apiJar(type: Jar, dependsOn: [':core:classes', ':core:javadoc']) {
    classifier = 'api'
    from project(':core').sourceSets.main.output
    from project(':core').sourceSets.main.allJava
    from project(':core').javadoc.destinationDir
}

artifacts {
    archives apiJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

task updateVersionInGradleProps() {
    File gradlePropsFile = project.file('gradle.properties')

    if (!gradlePropsFile.exists())
        gradlePropsFile.createNewFile()

    Properties props = new Properties()

    FileInputStream reader = new FileInputStream(gradlePropsFile)
    try {
        props.load(reader)
    } finally {
        reader.close()
    }

    props.setProperty("version", "${version}")

    FileOutputStream writer = new FileOutputStream(gradlePropsFile)
    try {
        props.store(writer, "NBTProxy properties")
    } finally {
        writer.close()
    }
}
commitNewVersion.dependsOn updateVersionInGradleProps

release {
    buildTasks = ['apiJar']
}
